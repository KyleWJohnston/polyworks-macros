version "5.0"
#=======================================================
# ---- InnovMetric Software Inc.
# ---- Module  :    PolyWorks|Inspector
# ---- Version :    2018 IR9 (build 6181)
# ---- Date    :    Wednesday, March 27, 2019 - 17:02:47
#-------------------------------------------------------

# Created by Kyle Johnston on 2019-03-27
# Last update: 2019-04-16

# Creates feature from 2 other features repetitively

# Example of alternating
# 01.1-1 (selected)
# 01.1-2 (selected)
# 01.2-1 (selected)
# 01.2-2 (selected)
# 01.3-1 (selected)
# 01.3-2 (selected, optional)

# Example of not alternating
# 01.1-1 (selected)
# 01.2-1 (selected)
# 01.3-1 (selected)
# 01.1-2
# 01.2-2
# 01.3-2


# Get when to start (index1) and stop (index2) from selected features
DECLARE names
DECLARE index1
DECLARE index2
TREEVIEW FEATURE NAME SELECTED GET ( names )
TREEVIEW FEATURE INDEX GET ( $names[1], index1 )
TREEVIEW FEATURE INDEX GET ( $names[SIZE ( names )], index2 )

# Get feature_increment from user (see above examples)
DECLARE alternating
MACRO INPUT QUESTION ( alternating, "Are the values alternating (instead of separate but consecutive groups)?" )

# Get feature type to create from user
DECLARE type
MACRO INPUT STRING ( type, "Select feature type", "Select type of feature to create: [d]istances, [p]oints", "d" )

# Set increments
DECLARE set_increment
DECLARE feature_increment
IF $alternating == 1
    SET set_increment 2
	SET feature_increment 1
ELSEIF $alternating == 0
    SET set_increment 1
    SET feature_increment SIZE ( names )
ENDIF

# Create new features
DECLARE i $index1
DECLARE top_name
DECLARE bot_name
WHILE $i <= $index2
    TREEVIEW FEATURE NAME GET ( EXPR ($i), top_name )
    TREEVIEW FEATURE NAME GET ( EXPR ($i+$feature_increment), bot_name )

	IF $type == "d"
        # Create distance feature
        FEATURE DISTANCE CREATE ( $bot_name, $top_name, "$top_name - dist" )

    ELSEIF $type == "p"
	    # Create average point feature
    	TREEVIEW OBJECT SELECT NONE
	    TREEVIEW FEATURE SELECT ( $i, "On" )
    	TREEVIEW FEATURE SELECT ( EXPR ($i+$feature_increment), "On" )
	    FEATURE PRIMITIVE POINT FROM_POINTS ( "$top_name - avg", )

    ENDIF

    # Increment i
    SET i EXPR ( $i+$set_increment )
ENDWHILE
