version "5.0"
#========================================================
# ---- InnovMetric Software Inc.
# ---- Module  :    PolyWorks|Inspector
# ---- Version :    2018 IR6 (build 5240)
# ---- Date    :    Thursday, December 6, 2018 - 11:29:23
#--------------------------------------------------------

# Created by Kyle Johnston on 2019-01-11
# Last update: 2019-06-18

# Exports PDF reports for a specified number of pieces and names them by piece name

# Forward slashes get replaced by underscores to prevent invalid file names


# Get number of reports to save from user
DECLARE number_of_reports
MACRO INPUT INTEGER ( number_of_reports, "Enter number of reports", "Enter the number of reports to export, starting from the most recent. Enter -1 to export all reports.", -1 )
IF $number_of_reports == "0"
    MACRO ECHO ( "EXIT: User canceled operation." )
    MACRO END ( )
ENDIF

# Get where to save reports from user
DECLARE save_dir
DECLARE workspace_path
MACRO INPUT FOLDER_PATH ( save_dir, "Select location to save reports" , "$workspace_path:h\PDF Reports" )
IF $save_dir == "0"
    MACRO ECHO ( "EXIT: No save path provided by user." )
    MACRO END ( )
ENDIF

# Get list of pieces
DECLARE piece_names
PIECE NAME ALL GET ( piece_names )

# Calculate first report to import
DECLARE i
IF $number_of_reports == -1
    SET i 1
ELSE
    SET i EXPR ( SIZE ( piece_names ) - $number_of_reports + 1 )
ENDIF

DECLARE piece_name_clean
DECLARE report_name
DECLARE report_name_clean
DECLARE error_status
DECLARE export_counter 0
DECLARE error_counter 0
WHILE $i <= SIZE ( piece_names )
    
    # Open piece
    PIECE OPEN ( $piece_names[${i}] )
    MACRO STRING REPLACE ( $piece_names[${i}], "/", "_", 1, , piece_name_clean )
    
    # Export PDF report
    MACRO SHARED_VARIABLE GET_VALUE ( "workspace_path", workspace_path )
    TREEVIEW FORMATTED_REPORT NAME GET ( 1, report_name )
    MACRO STRING REPLACE ( $report_name, "/", "_", 1, , report_name_clean )
    FILE EXPORT_FORMATTED_REPORT PDF ( "${save_dir}\${piece_name_clean}.pdf", "Off", $report_name_clean )
    MACRO GET_ERROR_STATUS ( error_status )
    
    # Increase i and export_counter before next piece
    ++i
    IF $error_status == "No Error"
        ++export_counter
    ELSE
        ++error_counter
    ENDIF
    
ENDWHILE

MACRO PAUSE ( "Macro complete", "Saved ${export_counter} report(s) in ${save_dir} with ${error_counter} error(s).")
